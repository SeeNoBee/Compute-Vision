macro(parse_arguments FUNCTION_NAME PREFIX)
    cmake_parse_arguments("${PREFIX}" "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    list(LENGTH ${PREFIX}_UNPARSED_ARGUMENTS UNPARSED_COUNT)
    if(UNPARSED_COUNT GREATER 0)
        message(WARNING "Unexpected arguments in function '${FUNCTION_NAME}': ${${PREFIX}_UNPARSED_ARGUMENTS}")
    endif()
endmacro()

function(parent RESULT)
    set(options WARNING)
    set(oneValueArgs PATH LEVEL)

    parse_arguments("parent" "ARGS" ${ARGN})

    if("${ARGS_LEVEL}" STREQUAL "")
        set(ARGS_LEVEL 1)
    elseif(${ARGS_LEVEL} EQUAL 0)
        set(${RESULT} ${ARGS_PATH} PARENT_SCOPE)
        return()
    elseif(${ARGS_LEVEL} LESS 0)
        if(ARGS_WARNING)
            message(WARNING "LEVEL argument of 'parent' function is [${ARGS_LEVEL}] < 0")
        endif()
        set(${RESULT} "" PARENT_SCOPE)
        return()
    endif()

    set(PATH ${ARGS_PATH})

    math(EXPR DEPTH "${ARGS_LEVEL} - 1")
    foreach(LEVEL RANGE ${DEPTH})
        set(TMP ${PATH})
        get_filename_component(PATH ${PATH} DIRECTORY)
        if(("${TMP}" STREQUAL "${PATH}") OR ("${PATH}" STREQUAL ""))
            if(ARGS_WARNING)
                message(WARNING "Parent directory of '${ARGS_PATH}' level [${LEVEL}] is not exist")
            endif()
            set(PATH "")
            break()
        endif()
    endforeach()

    set(${RESULT} ${PATH} PARENT_SCOPE)
endfunction(parent)

function(list_of_content RESULT)
    set(options RECURSE RELATIVE DIRECTORIES)
    set(multiValueArgs FILTERS PATHS)

    parse_arguments("list_of_content" "ARGS" ${ARGN})

    if(ARGS_RECURSE)
        set(GLOB_OPTION GLOB_RECURSE)
    else()
        set(GLOB_OPTION GLOB)
    endif()

    if(ARGS_DIRECTORIES)
        set(DIRECORIES_OPTION LIST_DIRECTORIES true)
    else()
        set(DIRECORIES_OPTION LIST_DIRECTORIES false)
    endif()

    if("${ARGS_FILTERS}" STREQUAL "")
        set(ARGS_FILTERS *)
    endif()

    foreach(PATH ${ARGS_PATHS})
        foreach(FILTER ${ARGS_FILTERS})
            if(ARGS_RELATIVE)
                set(RELATIVE_OPTION RELATIVE ${PATH})
            endif()
            file(${GLOB_OPTION} LOCAL_RESULT ${DIRECORIES_OPTION} ${RELATIVE_OPTION} "${PATH}/${FILTER}")
            list(APPEND LIST_OF_CONTENT ${LOCAL_RESULT})
        endforeach()
    endforeach()

    set(${RESULT} ${LIST_OF_CONTENT} PARENT_SCOPE)
endfunction(list_of_content)

function(is_found RESULT PATH)
    if("${PATH}" STREQUAL "")
        set(${RESULT} FALSE PARENT_SCOPE)
        return()
    endif()

    string(REGEX MATCH "-NOTFOUND$" MATCHED "${PATH}")
    if("${MATCHED}" STREQUAL "")
        set(${RESULT} TRUE PARENT_SCOPE)
    else()
        set(${RESULT} FALSE PARENT_SCOPE)
    endif()
endfunction(is_found)

function(find RESULT)
    set(options FATAL RELATIVE LOUD STATUS)
    set(oneValueArgs INDICATOR LEVEL DOC)
    set(multiValueArgs PATHS)

    file(TO_CMAKE_PATH "${ARGN}" ARGN)

    parse_arguments("find" "ARGS" ${ARGN})

    is_found(RESULT_FOUND "${${RESULT}}")
    if(RESULT_FOUND)
        return()
    endif()

    if(ARGS_STATUS)
        message(STATUS "Searching ${RESULT}")
    endif()

    if(ARGS_FATAL)
        set(MESSAGE_TYPE FATAL_ERROR)
    else()
        set(MESSAGE_TYPE WARNING)
    endif()

    if(ARGS_RELATIVE)
        set(RELATIVE_OPTION RELATIVE)
    endif()

    if("${ARGS_LEVEL}" STREQUAL "")
        set(ARGS_LEVEL 0)
    endif()

    if(${ARGS_LEVEL} EQUAL 0)
        set(CACHE_VAR_TYPE_OPTION FILEPATH)
    else()
        set(CACHE_VAR_TYPE_OPTION PATH)
    endif()

    if("${ARGS_DOC}" STREQUAL "")
        set(ARGS_DOC "Doc for ${RESULT} is undefined")
    endif()

    set(${RESULT} ${RESULT}-NOTFOUND CACHE ${CACHE_VAR_TYPE_OPTION} ${ARGS_DOC} FORCE)

    if("${ARGS_INDICATOR}" STREQUAL "")
        if(ARGS_LOUD)
            message(${MESSAGE_TYPE} "Indicator is undefined")
        endif()
        return()
    endif()

    foreach(PATH ${ARGS_PATHS})
        list_of_content(FINDS PATHS ${PATH} ${RELATIVE_OPTION} RECURSE FILTERS */${ARGS_INDICATOR})

        list(LENGTH FINDS FINDS_COUNT)

        if(${FINDS_COUNT} EQUAL 1)
            parent(TARGET_DIR PATH ${FINDS} LEVEL ${ARGS_LEVEL})
            if("${TARGET_DIR}" STREQUAL "")
                continue()
            else()
                set(${RESULT} ${TARGET_DIR} CACHE ${CACHE_VAR_TYPE_OPTION} ${ARGS_DOC} FORCE)
                break()
            endif()
        else()
            continue()
        endif()
    endforeach()
endfunction(find)

function(paths_are_exists RESULT)
    set(oneValueArgs PATHS)
    file(TO_CMAKE_PATH "${ARGN}" ARGN)
    parse_arguments("paths_are_exists" "ARGS" ${ARGN})
    if("${ARGS_PATHS}" STREQUAL "")
        set(${RESULT} FALSE PARENT_SCOPE)
        return()
    endif()

    set(${RESULT} TRUE PARENT_SCOPE)

    foreach(PATH ${ARGS_PATHS})
        if(NOT EXISTS ${PATH})
            set(${RESULT} FALSE PARENT_SCOPE)
            return()
        endif()
    endforeach()
endfunction(paths_are_exists)
